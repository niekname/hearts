plugins {
  alias(libs.plugins.kotlin.jvm)
  alias(libs.plugins.versions)
  id 'application'
}

repositories {
  mavenCentral()
}

dependencies {
  testImplementation(libs.junit.jupiter.api)
  testImplementation(libs.junit.jupiter.params)
  testRuntimeOnly(libs.junit.jupiter.engine)
  testImplementation(libs.assertj.core)
}

kotlin {
  jvmToolchain(21)
}

test {
  useJUnitPlatform()
}

tasks.named("dependencyUpdates").configure {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }

  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
